plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.40'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    minecraftVersion = '1.13.2-R0.1-SNAPSHOT'
    selfRevision = "1"
}

group 'de.eternalwings.bukkit'
version "${minecraftVersion.replace("-SNAPSHOT","")}-${selfRevision}"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "org.bukkit:bukkit:${minecraftVersion}"
    compileOnly "org.bukkit:craftbukkit:${minecraftVersion}"
    testCompile "org.bukkit:craftbukkit:${minecraftVersion}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ['-Xjvm-default=compatibility']
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ['-Xjvm-default=compatibility']
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.kotlin
            artifact sourcesJar
            groupId 'de.eternalwings.bukkit'
            artifactId 'ext'
            version project.version

            pom {
                name = 'Craftbukkit EXT'
                description = 'Provides Kotlin extension functions to make working with bukkit & nms a little easier.'
                url = 'https://github.com/kumpelblase2/craftbukkit-ext'
                licenses {
                    license {
                        name = "MIT"
                        url = "https://github.com/kumpelblase2/craftbukkit-ext/blob/master/LICENSE"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "kumpelblase2"
                        name = "Tim Hagemann"
                        email = "tim+github@eternalwings.de"
                    }
                }

                scm {
                    url = "https://github.com/kumpelblase2/craftbukkit-exit"
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_APIKEY')
    publications = ['mavenPublication']

    pkg {
        repo = 'Libraries'
        name = 'craftbukkit-ext'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/kumpelblase2/craftbukkit-ext.git'
        version {
            name = project.version
            released = new Date()
        }
    }
}
